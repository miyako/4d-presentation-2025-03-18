//%attributes = {}
#DECLARE($file : 4D:C1709.File; $privateKey : Text)

var $fileHandle : 4D:C1709.FileHandle
var $key : 4D:C1709.CryptoKey

var $documentAsBlob; $blobSignature : Blob
var $documentSize : Integer
var $digest; $signature : Text
var $keyOptions; $signOptions : Object
var $wp : Object


If (Count parameters:C259<2)  // ç§˜å¯†éµãŒæä¾›ã•ã‚Œã¦ã„ãªã„å ´åˆ
	
	If (ds:C1482.CryptoKey.all().length>0)
		$privateKey:=ds:C1482.CryptoKey.all().first().privateKey
		ok:=1
	Else 
		ALERT:C41("æš—å·éµã‚’å…ˆã«ç”Ÿæˆã—ã¦ãã ã•ã„")
		ok:=0
	End if 
	
Else 
	ok:=1
End if 

If (ok=1)
	
	// ç½²åã‚’æœ€å¾Œã«è¿½åŠ ã™ã‚‹ãŸã‚ã€ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’èª­ã¿æ›¸ããƒ¢ãƒ¼ãƒ‰ã§é–‹ãã¾ã™
	$fileHandle:=$file.open("write")  // ã‚ªãƒ•ã‚»ãƒƒãƒˆ = 0
	$documentSize:=$fileHandle.getSize()
	
	// ç½²åç”¨ã® digest ã‚’è¨ˆç®—ã—ã¾ã™
	$documentAsBlob:=$fileHandle.readBlob($documentSize)
	$digest:=Generate digest:C1147($documentAsBlob; SHA512 digest:K66:5)
	
	// ğŸ”ºğŸ”ºğŸ”º // digest è¨ˆç®—å¾Œã«ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’å¤‰æ›´ã—ã¾ã™
	$fileHandle:=Null:C1517
	$wp:=WP Import document:C1318($file)
	WP SET TEXT:C1574($wp; "x"; wk append:K81:179)
	WP EXPORT DOCUMENT:C1337($wp; $file)
	
	$fileHandle:=$file.open("write")  // ã‚ªãƒ•ã‚»ãƒƒãƒˆ = 0
	$documentSize:=$fileHandle.getSize()
	// ğŸ”ºğŸ”ºğŸ”º  >
	
	
	// ç§˜å¯†éµã«åŸºã¥ã„ã¦æ–°ã—ã„ã‚­ãƒ¼ã‚’ä½œæˆã—ã¾ã™
	$keyOptions:={type: "PEM"; pem: $privateKey}
	$key:=4D:C1709.CryptoKey.new($keyOptions)
	
	// .sign() é–¢æ•°ã‚’ä½¿ã£ã¦ç½²åã‚’ç”Ÿæˆã—ã¾ã™
	$signOptions:={hash: "SHA512"; encodingEncrypted: "Base64URL"}
	$signature:=$key.sign($digest; $signOptions)
	
	// ç½²åã®æœ€å¾Œã« "SIGN" ã‚’è¿½åŠ ã—ã¾ã™
	$signature:=$signature+String:C10(Length:C16($signature); "000000")+"SIGN"  // ç½²åã®æœ€å¾Œã® 10æ–‡å­—ï¼š "000789SIGN"
	
	// BLOBå½¢å¼ã®ç½²åã‚’ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã®æœ€å¾Œã«è¿½åŠ ã—ã¾ã™
	CONVERT FROM TEXT:C1011($signature; "UTF-8"; $blobSignature)
	
	$fileHandle.offset:=$documentSize
	$fileHandle.writeBlob($blobSignature)
	$fileHandle:=Null:C1517
	
End if 